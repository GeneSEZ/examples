module org.genesez.metamodel.workflow.transformation

// properties
var uml2.file = "modelcache/website.uml"
var gpres.file = "modelcache/website.gpresentation.xmi"

Workflow {
	// initialize uml2 support
	bean = org.eclipse.xtend.typesystem.uml2.Setup {
		standardUML2Setup = true
	}
	
	// reference to the uml meta model
	bean = org.eclipse.xtend.typesystem.uml2.UML2MetaModel : uml { 
	}
	
	// reference to the gpresentation meta model
	bean = org.eclipse.xtend.typesystem.emf.EmfMetaModel : gpres {
		metaModelPackage = "org.genesez.metamodel.gpresentation.GpresentationPackage"
	}

	// instantiate the uml model from file
	component = org.eclipse.xtend.typesystem.emf.XmiReader {
		modelFile = "${uml2.file}"
		outputSlot = "uml2model"
	}
	component = org.eclipse.xtend.check.CheckComponent {
		metaModel = uml
		checkFile = "org::genesez::adapter::uml2::uml2presentation"
		emfAllChildrenSlot = "uml2model"
	}
	// transform the uml model into an gpresenation model
	component = org.eclipse.xtend.XtendComponent {
		metaModel = gpres
		metaModel = uml
		invoke = "org::genesez::adapter::uml2::uml2presentation::transform(uml2model)"
		outputSlot = "gpresModel"
	}
	// serialize the instantiated ecore model into a file
	component = org.eclipse.xtend.typesystem.emf.XmiWriter {
		inputSlot = "gpresModel"
		modelFile = "${gpres.file}"
	}
}